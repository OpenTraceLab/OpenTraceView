##
## This file is part of the OpenTraceView project.
##
## Copyright (C) 2013-2020 Uwe Hermann <uwe@hermann-uwe.de>
##
## This program is free software; you can redistribute it and/or modify
## it under the terms of the GNU General Public License as published by
## the Free Software Foundation; either version 2 of the License, or
## (at your option) any later version.
##
## This program is distributed in the hope that it will be useful,
## but WITHOUT ANY WARRANTY; without even the implied warranty of
## MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
## GNU General Public License for more details.
##
## You should have received a copy of the GNU General Public License
## along with this program; if not, see <http://www.gnu.org/licenses/>.
##

#
# This file is used to create the OpenTraceView Windows installer via NSIS.
# It is meant for use in a cross-compile setup (not for native builds).
#
# NSIS documentation:
# http://nsis.sourceforge.net/Docs/
# http://nsis.sourceforge.net/Docs/Modern%20UI%202/Readme.html
#

# Include the "Modern UI" header, which gives us the usual Windows look-n-feel.
!include "MUI2.nsh"
!include "EnvVarUpdate.nsh"


# --- Global stuff ------------------------------------------------------------

# Installer/product name.
Name "OpenTraceView"

# Filename of the installer executable.
OutFile "opentraceview-@OTV_VERSION_STRING@-installer.exe"

# Where to install the application.
InstallDir "$PROGRAMFILES\sigrok\OpenTraceView"

# Request admin privileges for Windows Vista and Windows 7.
# http://nsis.sourceforge.net/Docs/Chapter4.html
RequestExecutionLevel admin

# Local helper definitions.
!define REGSTR "Software\Microsoft\Windows\CurrentVersion\Uninstall\OpenTraceView"


# --- MUI interface configuration ---------------------------------------------

# Use the following icon for the installer EXE file.
!define MUI_ICON "@PROJECT_SOURCE_DIR@/icons/opentraceview.ico"

# Show a nice image at the top of each installer page.
!define MUI_HEADERIMAGE

# Don't automatically go to the Finish page so the user can check the log.
!define MUI_FINISHPAGE_NOAUTOCLOSE

# Upon "cancel", ask the user if he really wants to abort the installer.
!define MUI_ABORTWARNING

# Don't force the user to accept the license, just show it.
# Details: http://trac.videolan.org/vlc/ticket/3124
!define MUI_LICENSEPAGE_BUTTON $(^NextBtn)
!define MUI_LICENSEPAGE_TEXT_BOTTOM "Click Next to continue."

# Path where the cross-compiled sigrok tools and libraries are located.
# Change this to where-ever you installed libsigrok.a and so on.
!define CROSS "@CMAKE_INSTALL_PREFIX@"

# Defines for WinVer.nsh.
!define WINVER_2000	0x0500
!define WINVER_XP	0x0501
!define WINVER_2003	0x0502
!define WINVER_VISTA	0x0600
!define WINVER_7	0x0601
!define WINVER_8	0x0602
!define WINVER_8.1	0x0603
!define WINVER_10	0x0A00


# --- MUI pages ---------------------------------------------------------------

# Show a nice "Welcome to the ... Setup Wizard" page.
!insertmacro MUI_PAGE_WELCOME

# Show the license of the project.
!insertmacro MUI_PAGE_LICENSE "@PROJECT_SOURCE_DIR@/COPYING"

# Show a screen which allows the user to select which components to install.
!insertmacro MUI_PAGE_COMPONENTS

# Allow the user to select a different install directory.
!insertmacro MUI_PAGE_DIRECTORY

# Perform the actual installation, i.e. install the files.
!insertmacro MUI_PAGE_INSTFILES

# Show a final "We're done, click Finish to close this wizard" message.
!insertmacro MUI_PAGE_FINISH

# Pages used for the uninstaller.
!insertmacro MUI_UNPAGE_WELCOME
!insertmacro MUI_UNPAGE_CONFIRM
!insertmacro MUI_UNPAGE_INSTFILES
!insertmacro MUI_UNPAGE_FINISH


# --- MUI language files ------------------------------------------------------

# Select an installer language (required!).
!insertmacro MUI_LANGUAGE "English"


# --- Default section ---------------------------------------------------------

Section "OpenTraceView (required)" Section1
	# This section is gray (can't be disabled) in the component list.
	SectionIn RO

	# Install the file(s) specified below into the specified directory.
	SetOutPath "$INSTDIR"

	# License file.
	File "@PROJECT_SOURCE_DIR@/COPYING"

	# OpenTraceView (statically linked, includes all libs).
	File "${CROSS}/bin/opentraceview.exe"

	# Zadig (used for installing WinUSB drivers).
	File "${CROSS}/zadig.exe"
	File "${CROSS}/zadig_xp.exe"

	# Python
	File "${CROSS}/python34.dll"
	File "${CROSS}/python34.zip"

	# Protocol decoders.
	SetOutPath "$INSTDIR\decoders"
	File /r /x "__pycache__" "${CROSS}/share/libsigrokdecode/decoders/*"

	# Firmware files.
	SetOutPath "$INSTDIR\firmware"
	File /r "${CROSS}/share/sigrok-firmware/*"

	# Generate the uninstaller executable.
	WriteUninstaller "$INSTDIR\Uninstall.exe"

	# Create a sub-directory in the start menu.
	CreateDirectory "$SMPROGRAMS\sigrok"
	CreateDirectory "$SMPROGRAMS\sigrok\OpenTraceView"

	# Create a shortcut for the OpenTraceView application.
	CreateShortCut "$SMPROGRAMS\sigrok\OpenTraceView\OpenTraceView.lnk" \
		"$INSTDIR\opentraceview.exe" "" "$INSTDIR\opentraceview.exe" \
		0 SW_SHOWNORMAL \
		"" "Open-source, portable logic analyzer software"

	# Create a shortcut for the OpenTraceView application via Zadig.
	CreateShortCut "$SMPROGRAMS\sigrok\OpenTraceView\Zadig (Win 7+).lnk" \
		"$INSTDIR\zadig.exe" "" "$INSTDIR\zadig.exe" 0 SW_SHOWNORMAL \
		"" "Zadig (Win 7+)"
	CreateShortCut "$SMPROGRAMS\sigrok\OpenTraceView\Zadig (Win XP).lnk" \
		"$INSTDIR\zadig_xp.exe" "" "$INSTDIR\zadig_xp.exe" 0 \
		SW_SHOWNORMAL "" "Zadig (Win XP)"

	# Create a shortcut for the uninstaller.
	CreateShortCut "$SMPROGRAMS\sigrok\OpenTraceView\Uninstall.lnk" \
		"$INSTDIR\Uninstall.exe" "" "$INSTDIR\Uninstall.exe" 0 \
		SW_SHOWNORMAL "" "Uninstall OpenTraceView"

	# Create registry keys for "Add/remove programs" in the control panel.
	WriteRegStr HKLM "${REGSTR}" "DisplayName" "OpenTraceView"
	WriteRegStr HKLM "${REGSTR}" "UninstallString" \
		"$\"$INSTDIR\Uninstall.exe$\""
	WriteRegStr HKLM "${REGSTR}" "InstallLocation" "$\"$INSTDIR$\""
	WriteRegStr HKLM "${REGSTR}" "DisplayIcon" \
		"$\"$INSTDIR\opentraceview.exe$\""
	WriteRegStr HKLM "${REGSTR}" "Publisher" "sigrok"
	WriteRegStr HKLM "${REGSTR}" "HelpLink" \
		"http://sigrok.org/wiki/OpenTraceView"
	WriteRegStr HKLM "${REGSTR}" "URLUpdateInfo" \
		"http://sigrok.org/wiki/Downloads"
	WriteRegStr HKLM "${REGSTR}" "URLInfoAbout" "http://sigrok.org"
	WriteRegStr HKLM "${REGSTR}" "DisplayVersion" "@OTV_VERSION_STRING@"
	WriteRegStr HKLM "${REGSTR}" "Contact" \
		"sigrok-devel@lists.sourceforge.net"
	WriteRegStr HKLM "${REGSTR}" "Comments" \
		"This is a Qt based logic analyzer, oscilloscope, and MSO GUI."

	# Display "Remove" instead of "Modify/Remove" in the control panel.
	WriteRegDWORD HKLM "${REGSTR}" "NoModify" 1
	WriteRegDWORD HKLM "${REGSTR}" "NoRepair" 1
SectionEnd


# --- Uninstaller section -----------------------------------------------------

Section "Uninstall"
	# Always delete the uninstaller first (yes, this really works).
	Delete "$INSTDIR\Uninstall.exe"

	# Delete the application, the application data, and related libs.
	Delete "$INSTDIR\COPYING"
	Delete "$INSTDIR\opentraceview.exe"
	Delete "$INSTDIR\zadig.exe"
	Delete "$INSTDIR\zadig_xp.exe"
	Delete "$INSTDIR\python34.dll"
	Delete "$INSTDIR\python34.zip"

	# Delete all decoders and firmware files.
	RMDir /r "$INSTDIR\decoders\*"
	RMDir /r "$INSTDIR\firmware\*"

	# Delete the install directory and its sub-directories.
	RMDir "$INSTDIR\decoders"
	RMDir "$INSTDIR\firmware"
	RMDir "$INSTDIR"

	# Delete the links from the start menu.
	Delete "$SMPROGRAMS\sigrok\OpenTraceView\OpenTraceView.lnk"
	Delete "$SMPROGRAMS\sigrok\OpenTraceView\Zadig (Win 7+).lnk"
	Delete "$SMPROGRAMS\sigrok\OpenTraceView\Zadig (Win XP).lnk"
	Delete "$SMPROGRAMS\sigrok\OpenTraceView\Uninstall.lnk"

	# Delete the sub-directory in the start menu.
	RMDir "$SMPROGRAMS\sigrok\OpenTraceView"
	RMDir "$SMPROGRAMS\sigrok"

	# Delete the registry key(s).
	DeleteRegKey HKLM "${REGSTR}"
SectionEnd
